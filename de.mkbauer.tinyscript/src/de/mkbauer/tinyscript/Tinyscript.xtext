grammar de.mkbauer.tinyscript.Tinyscript hidden(WS, ML_COMMENT, SL_COMMENT)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate ts "http://www.mkbauer.de/tinyscript/Tinyscript"

Tinyscript:
	elements=SourceElements
;

SourceElements:
	{SourceElements}
	(statements += Statement)* 
;

Block: 
	'{' sourceelements=SourceElements '}'	
;

Statement:
	(=> FunctionDeclaration) | 
	VariableStatement | 
	ExpressionStatement | 
	ReturnStatement | 
	IfStatement |
	ForEachStatement |
	EmptyStatement
;

EmptyStatement:
	';'
;

FunctionDeclaration:
	'function' ( name=ID ) '(' params=FormalParameterList? ')' block=Block 
;

ExpressionStatement:
	AssignmentExpression ';'
;

// TODO: Simplify expression hierarchy:
// - Use tree rewrites to use BinaryOperation, UnaryOperation
// - Split compare into equality and relational expressions  
// see https://github.com/eclipse/xtext/blob/master/plugins/org.eclipse.xtext.xbase/src/org/eclipse/xtext/xbase/Xbase.xtext
AssignmentExpression returns Expression:
	EqualityExpression ({BinaryExpression.left=current} op=OP_ASSIGN right=AssignmentExpression)?	
;

EqualityExpression returns Expression:
	CompareExpression ({BinaryExpression.left=current} op=OP_EQUALS right=CompareExpression)?
;

CompareExpression returns Expression:
	OrExpression ({BinaryExpression.left=current} op=OP_COMP right=OrExpression)?
;

OrExpression returns Expression:
	AndExpression ({BinaryExpression.left=current} op=('||') right=AndExpression)*
;

AndExpression returns Expression:
	Addition  ({BinaryExpression.left=current} op=('&&') right=Addition)*
;

Addition returns Expression:
	Multiplication ({BinaryExpression.left=current} op=('+' | '-') right=Multiplication)*
;

Multiplication returns Expression:
    (Unary | Primary) ({BinaryExpression.left=current} op=('*' | '/') right=(Unary | Primary))*
;

Unary:
	(op='-'|OP_NOT) expr=Primary // ExpressionStatement?
;

Primary:
	(expr=Atomic  | '(' expr=AssignmentExpression ')' ) (suffixes+=CallOrMemberSuffix)*  
;

Atomic:
	VariableOrMember |
	BooleanLiteral |
	NumberLiteral |
    StringLiteral |
    ObjectInitializer |
    ArrayInitializer | 
    FunctionExpression
;

FunctionExpression:
	'function' ( name=ID )? '(' params=FormalParameterList? ')' block=Block 
;


FormalParameterList:
	(params += Variable) (',' params += Variable)*
;

ReturnStatement:
	{Return}
	'return' (expr=AssignmentExpression)? ';'
;

IfStatement:
	'if' '(' cond=AssignmentExpression ')' then=Block
	('else' else=Block)?
;

ForEachStatement:
	'foreach' '(' var=Variable 'in' inexpr=AssignmentExpression ')' do=Block 
;

VariableStatement:
	'var' vardecllist=VariableDeclarationList ';'
;

VariableDeclarationList:
	(vardecls += VariableDeclaration) (',' vardecls += VariableDeclaration)*
;

VariableDeclaration:
	var=Variable (OP_ASSIGN expr=AssignmentExpression)?
;

VariableOrMember:
	var=[Variable] | this?='this'  
;

Variable:
	name=ID
;

// TODO: Remove rule
CallOrMemberSuffix:
	{CallOrMemberSuffix}
	((membersuffix = MemberSuffix) | (callsuffix = CallSuffix))
;

CallSuffix:
	{CallSuffix}
	'(' arguments=ArgumentList? ')' 
;

ArgumentList:
	(arguments += AssignmentExpression) (',' arguments += AssignmentExpression)*
;

MemberSuffix:
	('[' expr=Addition ']' | // TODO: AssignmentExpression?
	'.' name=ID ) 
;

ObjectInitializer:
	{ObjectInitializer}
	'{' (propertyassignments+=PropertyAssignment)? (',' propertyassigments+=PropertyAssignment)* '}'
;

PropertyAssignment:
	key=PropertyName ':' value=AssignmentExpression
;

PropertyName:
	expr=StringLiteral |
	name=ID
;

ArrayInitializer:
	{ArrayInitializer}
	'[' (values+=Atomic)? (',' values += Atomic)* ']'  
;

StringLiteral:
	value=STRING
;

// TODO: Add boolean literal
BooleanLiteral:
	value=BOOLEAN
;

NumberLiteral:
	value=DOUBLE
;

terminal OP_EQUALS:
	'==' | '===' | '!=' | '!=='
;

terminal OP_COMP:
	'==' | '===' | '!=' | '<' | '<=' | '>' | '>='
;

terminal OP_ASSIGN:
	'='
;

terminal OP_NOT:
	'!'
;

terminal BOOLEAN returns ecore::EBoolean:
	'true' | 'false'
;

terminal DOUBLE returns ecore::EDouble:
    (('0'..'9')+ ('.' ('0'..'9')*)? | '.' ('0'..'9')+)
    (('e' | 'E') ('+' | '-')? ('0'..'9')+)?;
    
terminal ID  		: '^'?('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'0'..'9')*;

terminal STRING	: 
			'"' ( '\\' ('b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\') | !('\\'|'"') )* '"' |
			"'" ( '\\' ('b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\') | !('\\'|"'") )* "'"
		; 
terminal ML_COMMENT	: '/*' -> '*/';
terminal SL_COMMENT 	: '//' !('\n'|'\r')* ('\r'? '\n')?;

terminal WS			: (' '|'\t'|'\r'|'\n')+;

terminal ANY_OTHER: .;