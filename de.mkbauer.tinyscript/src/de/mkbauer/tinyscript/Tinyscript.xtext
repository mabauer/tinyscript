grammar de.mkbauer.tinyscript.Tinyscript hidden(WS, ML_COMMENT, SL_COMMENT)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate ts "http://www.mkbauer.de/tinyscript/Tinyscript"

Tinyscript :
	global=Global
;

Global returns Block:
	{Block}
	(statements += Statement)*
;

Block: 
	{Block}
	'{' (statements += Statement)* '}'	
;

Statement:
	(=> FunctionDeclaration) | 
	(=> Block) |
	VariableStatement | 
	ExpressionStatement | 
	ReturnStatement | 
	IfStatement |
	ForEachStatement |
	BreakStatement |
	AssertStatement |
	EmptyStatement
;

EmptyStatement:
	';'
;

FunctionDeclaration returns Function:
	'function' ( id=Identifier ) '(' (params += Identifier)? (',' params += Identifier)* ')' block=Block 
;

ExpressionStatement:
	AssignmentExpression ';'
;

// TODO: Simplify expression hierarchy:
// - Use tree rewrites to use BinaryOperation, UnaryOperation
// - Split compare into equality and relational expressions  
// see https://github.com/eclipse/xtext/blob/master/plugins/org.eclipse.xtext.xbase/src/org/eclipse/xtext/xbase/Xbase.xtext
AssignmentExpression returns Expression:
	OrExpression ({BinaryExpression.left=current} op=OP_ASSIGN right=AssignmentExpression)?	
;

OrExpression returns Expression:
	AndExpression ({BinaryExpression.left=current} op=('||') right=AndExpression)*
;

AndExpression returns Expression:
	EqualityExpression  ({BinaryExpression.left=current} op=('&&') right=EqualityExpression)*
;

EqualityExpression returns Expression:
	CompareExpression ({BinaryExpression.left=current} op=OP_EQUALS right=CompareExpression)?
;

CompareExpression returns Expression:
	Addition ({BinaryExpression.left=current} op=OP_COMP right=Addition)?
;

Addition returns Expression:
	Multiplication ({BinaryExpression.left=current} op=('+' | '-') right=Multiplication)*
;

Multiplication returns Expression:
    (Unary | Primary) ({BinaryExpression.left=current} op=('*' | '/') right=(Unary | Primary))*
;

Unary:
	(op='-'|OP_NOT) expr=Primary // ExpressionStatement?
;

Primary returns Expression:
	(Atomic  | '(' AssignmentExpression ')' ) ({CallOrPropertyAccess.expr=current} suffixes+=CallOrMemberSuffix)*  
;

Atomic:
	Reference |
	BooleanLiteral |
	NumberLiteral |
    StringLiteral |
    ObjectInitializer |
    ArrayInitializer | 
    FunctionExpression
;

FunctionExpression returns Function:
	'function' ( id=Identifier )? '(' (params += Identifier)? (',' params += Identifier)* ')' block=Block 
;

ReturnStatement:
	{ReturnStatement}
	'return' (expr=AssignmentExpression)? ';'
;

AssertStatement:
	'assert' cond=AssignmentExpression ';'
;

IfStatement:
	'if' '(' cond=AssignmentExpression ')' then=Block
	('else' else=Block)?
;

ForEachStatement:
	'foreach' '(' (('var' id=Identifier) | ref=Reference) 'in' inexpr=AssignmentExpression ')' do=Block 
;

BreakStatement:
	{BreakStatement}
	'break' ';'
;

VariableStatement:
	'var' (vardecls += VariableDeclaration) (',' vardecls += VariableDeclaration)* ';'
;

VariableDeclaration returns Expression:
	Identifier ({BinaryExpression.left=current} op=OP_ASSIGN right=AssignmentExpression)?
;

Reference:
	id=[Identifier] | this?='this'  
;

Identifier:
	name=ID
;

CallOrMemberSuffix:
	((MemberSuffix) | (CallSuffix))
;

CallSuffix:
	{CallSuffix}
	'(' (arguments += AssignmentExpression)? (',' arguments += AssignmentExpression)* ')' 
;

MemberSuffix:
	('[' expr=Addition ']' | // TODO: AssignmentExpression?
	'.' name=ID ) 
;

ObjectInitializer:
	{ObjectInitializer}
	'{' (propertyassignments+=PropertyAssignment)? (',' propertyassigments+=PropertyAssignment)* '}'
;

PropertyAssignment:
	key=PropertyName ':' value=AssignmentExpression
;

PropertyName:
	expr=StringLiteral |
	name=ID
;

ArrayInitializer:
	{ArrayInitializer}
	'[' (values+=Atomic)? (',' values += Atomic)* ']'  
;

StringLiteral:
	value=STRING
;

BooleanLiteral:
	value=BOOLEAN
;

NumberLiteral:
	value=DOUBLE
;

terminal OP_EQUALS:
	'==' | '===' | '!=' | '!=='
;

terminal OP_COMP:
    '<' | '<=' | '>' | '>='
;

terminal OP_ASSIGN:
	'='
;

terminal OP_NOT:
	'!'
;

terminal BOOLEAN returns ecore::EBoolean:
	'true' | 'false'
;

terminal DOUBLE returns ecore::EDouble:
    (('0'..'9')+ ('.' ('0'..'9')*)? | '.' ('0'..'9')+)
    (('e' | 'E') ('+' | '-')? ('0'..'9')+)?;
    
terminal ID  		: '^'?('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'0'..'9')*;

terminal STRING	: 
			'"' ( '\\' ('b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\') | !('\\'|'"') )* '"' |
			"'" ( '\\' ('b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\') | !('\\'|"'") )* "'"
		; 
terminal ML_COMMENT	: '/*' -> '*/';
terminal SL_COMMENT 	: '//' !('\n'|'\r')* ('\r'? '\n')?;

terminal WS			: (' '|'\t'|'\r'|'\n')+;

terminal ANY_OTHER: .;