grammar de.mkbauer.tinyscript.Tinyscript with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate ts "http://www.mkbauer.de/tinyscript/Tinyscript"

Tinyscript:
	elements=SourceElements
;

SourceElements:
	{SourceElements}
	(statements += Statement)? (';' statements += Statement)* (';')?
;

Block: 
	'{' sourceelements=SourceElements '}'	
;

Statement:
	VariableStatement | ExpressionStatement | ReturnStatement // | => FunctionDeclaration
;

FunctionDeclaration:
	'function' ( name=ID ) '(' params=FormalParameterList? ')' block=Block 
;

ExpressionStatement:
	AssignmentExpression 
;

AssignmentExpression:
	lhs=CompareExpression (op=OP_ASSIGN rhs=ExpressionStatement)?	
;

CompareExpression:
	expr1=OrExpression (op=OP_COMP expr2 += OrExpression)?
;

OrExpression:
	expr1=AndExpression (ops+=('||') exprs += AndExpression)*
;

AndExpression:
	expr1=Addition  (ops+=('&&') exprs += Addition)*
;

Addition:
	expr1=Multiplication (ops+=('+' | '-') exprs += Multiplication)*
;

Multiplication:
    expr1=Unary  (ops+=('*' | '/') exprs += Unary)*
;

Unary:
	(op='-'|OP_NOT)? expr=Primary // ExpressionStatement?
;

Primary :
	(expr = Atomic  | '(' expr=ExpressionStatement ')') 
    (suffixes+=CallOrMemberSuffix)*
;

Atomic:
	VariableOrMember |
	IntegerLiteral |
	DoubleLiteral |
    StringLiteral |
    ObjectInitializer |
    ArrayInitializer | 
    FunctionExpression 
;

FunctionExpression:
	'function' ( name=ID )? '(' params=FormalParameterList? ')' block=Block 
;


FormalParameterList:
	(params += Variable) (',' params += Variable)*
;

ReturnStatement:
	{Return}
	'return' (expr=AssignmentExpression)?
;

VariableStatement:
	'var' vardecllist=VariableDeclarationList
;

VariableDeclarationList:
	(vardecls += VariableDeclaration) (',' vardecls += VariableDeclaration)*
;

VariableDeclaration:
	var=Variable (OP_ASSIGN expr=AssignmentExpression)?
;

VariableOrMember:
	var=[Variable] | this?='this'  
;

Variable:
	name=ID
;

CallOrMemberSuffix:
	{CallOrMemberSuffix}
	((membersuffixes += MemberSuffix) | (callsuffixes += CallSuffix))
;

CallSuffix:
	{CallSuffix}
	'(' arguments=ArgumentList? ')' 
;

ArgumentList:
	(arguments += AssignmentExpression) (',' arguments += AssignmentExpression)*
;

MemberSuffix:
	('[' expr=Addition ']' | // TODO: AssignmentExpression?
	'.' name=ID ) 
;

ObjectInitializer:
	{ObjectInitializer}
	'{' (propertyassignments+=PropertyAssignment)? (',' propertyassigments+=PropertyAssignment)* '}'
;

PropertyAssignment:
	key=PropertyName ':' value=AssignmentExpression
;

PropertyName:
	expr=StringLiteral |
	name=ID
;

ArrayInitializer:
	{ArrayInitializer}
	'[' (values+=Atomic)? (',' values += Atomic)* ']'  
;

StringLiteral:
	value=STRING
;

// TODO: Remove
IntegerLiteral:
    value=INT
;

DoubleLiteral:
	value=DOUBLE
;

terminal OP_COMP:
	'==' | '===' | '!=' | '<' | '<=' | '>' | '>='
;

terminal OP_ASSIGN:
	'='
;

terminal OP_NOT:
	'!'
;

terminal DOUBLE returns ecore::EDouble:
    (('0'..'9')+ ('.' ('0'..'9')*) | '.' ('0'..'9')+)
    (('e' | 'E') ('+' | '-')? ('0'..'9')+)?;